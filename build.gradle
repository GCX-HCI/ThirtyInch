// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    ext.kotlin_version = "1.3.21"
    repositories {
        google()
        jcenter()
    }
    dependencies {
        classpath "com.android.tools.build:gradle:3.3.1" // if you update this, also update the lintVersion below
        classpath "com.vanniktech:gradle-android-junit-jacoco-plugin:0.11.0"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath 'guru.stefma.bintrayrelease:bintrayrelease:1.1.1'
    }
}

apply plugin: 'com.vanniktech.android.junit.jacoco'

allprojects {
    repositories {
        google()
        jcenter()
    }
    tasks.withType(Javadoc) {
        options.addStringOption('Xdoclint:none', '-quiet')
        options.addStringOption('encoding', 'UTF-8')
        excludes = ['**/*.kt']
    }
}

ext {
    VERSION_NAME = "0.10.0-SNAPSHOT"
    VERSION_CODE = 11
    MIN_SDK_VERSION = 14
    // If you change the SDK version don't forgot to update .travis.yml
    TARGET_SDK_VERSION = 26
    COMPILE_SDK_VERSION = 26

    supportLibraryVersion = '26.0.0'
    // compositeAndroidVersion should be always the same version as the supportLib version
    // Since composite is not up to date we can't use the latest one
    compositeAndroidVersion = supportLibraryVersion

    junitVersion = '4.12'
    mockitoVersion = '2.10.0'
    assertjVersion = '2.8.0'
    supportTestVersion = '1.0.1'
    espressoVersion = '3.0.1'

    // According to https://github.com/googlesamples/android-custom-lint-rules/tree/master/android-studio-3
    // the lint version should match to the used Android Gradle Plugin by the formula "AGP Version X.Y.Z + 23.0.0"
    // E.g. "AGP Version 3.1.3 + 23.0.0 = Lint Version 26.1.3"
    lintVersion = '26.3.1'
}

allprojects {
    version = VERSION_NAME
    group = 'net.grandcentrix.thirtyinch'
}
