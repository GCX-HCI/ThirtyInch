buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath "com.novoda:bintray-release:$bintrayVersion"
    }
}

apply plugin: 'com.android.library'
apply plugin: 'bintray-release'
apply plugin: 'jacoco-android'

android {
    compileSdkVersion COMPILE_SDK_VERSION
    buildToolsVersion BUILD_TOOLS_VERSION

    defaultConfig {
        minSdkVersion MIN_SDK_VERSION
        targetSdkVersion TARGET_SDK_VERSION
        versionCode VERSION_CODE
        versionName VERSION_NAME
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        consumerProguardFiles 'proguard-rules.txt'
    }
    buildTypes {
        release {
            minifyEnabled false
        }
        debug {
            // output coverage with ./gradlew clean build createDebugCoverageReport
            testCoverageEnabled true
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }
    lintOptions {
        abortOnError false
    }
}

configurations {
    lintChecks
}

dependencies {
    provided "com.android.support:appcompat-v7:$supportLibraryVersion"

    lintChecks project(path: ':lint', configuration: 'lintChecks')

    testCompile "junit:junit:$junitVersion"
    testCompile "org.mockito:mockito-core:$mockitoVersion"
    testCompile "org.assertj:assertj-core:$assertjVersion"

    androidTestCompile 'com.android.support.test:runner:0.5'
    androidTestCompile "org.mockito:mockito-core:$mockitoVersion"
}

task copyLintJar(type: Copy) {
    from(configurations.lintChecks) {
        rename { 'lint.jar' }
    }
    into 'build/intermediates/lint/'
}

project.afterEvaluate {
    def compileLintTask = project.tasks.find { it.name == 'compileLint' }
    compileLintTask.dependsOn(copyLintJar)
}

publish {
    userOrg = 'passsy'
    groupId = 'net.grandcentrix.thirtyinch'
    publishVersion = VERSION_NAME
    artifactId = 'thirtyinch'
    uploadName = 'ThirtyInch'
    description = 'a Model View Presenter library for Android'
    website = 'https://github.com/grandcentrix/ThirtyInch'
}
